*:/sethome [$] = >>>
	if (import('sharedata.source') != 'creative') {
		_assertperm('restricted', 1)
	}
	assign(@modes, array('public','private','unlisted'))
	assign(@m, reg_split(' ', $))
	assign(@uuid, _get_puuid(player()))
	assign(@name, if(equals(@m[0], ''), 'default', @m[0]))
	assign(@mode, if(equals(@name, 'default'), 'private',
		if(equals(array_size(@m), 2), @m[1], 'unlisted')))
	if (array_contains(@modes, @mode)){
		try(
			store_value(concat('homes.', @uuid, '.', @name, '.loc'), ploc())
			store_value(concat('homes.', @uuid, '.', @name, '.mode'), @mode)
			msg(color('gray') . 'Home set.')
		,@ex,
			msg(color('red') . 'Home names must not contain special characters')
		);
	} else {
		msg(concat(color('red'), 'Usage: /sethome [name] [', array_implode(@modes,'|'), ']'))
	}
<<<


*:/delhome = >>>
        if (import('sharedata.source') != 'creative') {
                _assertperm('restricted', 1);
        }
        if(has_permission('nerdch.level.restricted')) {
                msg(concat(color('red'), 'Usage: /delhome [player] <name>'));
        } else {
                msg(concat(color('red'), 'Usage: /delhome <name>'));
        }
        call_alias(/listhomes);
<<<

*:/delhome $home = >>>
        if (import('sharedata.source') != 'creative') {
                _assertperm('restricted', 1);
        }
        @str = concat('homes.', _get_puuid(player()), '.', $home, '.loc');;

        if(has_value(@str)) {
            clear_value(@str);
            msg('Home' $home 'cleared.');
        } else {
            msg(concat(color('red'), 'Home ' , $home, ' not found.'));
            call_alias(/listhomes);
        }
<<<

*:/delhome $player $home = >>>
        _assertperm('restricted', 1)
        @str = concat('homes.', _get_puuid($player), '.', $home, '.loc');

        if(has_value(@str)) {
            clear_value(@str);
            msg('Home' $home 'cleared.');
        } else {
            msg(concat(color('red'), 'Home ' , $home, ' not found.'));
            call_alias(/listhomes $player);
        }
<<<


*:/home [$] = >>>
	if (import('sharedata.source') != 'creative') {
		_assertperm('restricted', 1)
	}

    assign(@m, reg_split(' ', $))
    if(equals(array_size(@m), 2),
        assign(@player, @m[0])
        array_remove(@m, 0),
    #else:
        assign(@player, strip_colors(player())))
    
    assign(@name,
        if(equals(@m[0], ''),
            'default',
            @m[0]))
    
    assign(@prefix, concat('homes.', _get_puuid(@player), '.', @name, '.'))
    
    if(has_value(concat(@prefix, 'loc')),
        if(or(
            equals(@player, strip_colors(player())),
            array_contains(array('public', 'unlisted'), get_value(concat(@prefix, 'mode'))),
            has_permission('homes.override')),
            
            set_ploc(get_value(concat(@prefix, 'loc'))),
        #else:
            msg(concat(color('red'), 'You do not have permission to visit that home'))),
    #else:
        msg(concat(color('red'), 'Unknown home!'))
        call_alias(/listhomes))

<<<

*:/listhomes [$] = >>>
	if (import('sharedata.source') != 'creative') {
		_assertperm('restricted', 1)
	}

    assign(@player,
        if(equals($, ''),
            strip_colors(player()),
            $))
    assign(@uuid, _get_puuid(@player))
    msg(concat(color('gray'), if(equals(@player, strip_colors(player())), 'Your', concat(@player, '\'s')), ' homes:'))
    if(or(
        equals(@player, strip_colors(player())),
        has_permission('homes.override')),
        
        
        foreach(array_keys(get_values(concat('homes.', @uuid))), @name,
            assign(@m, reg_match('^homes\\..+\\.(.+)\\.loc$', @name))
            if(array_index_exists(@m,0),
                msg(concat(color('gray'), '  ', @m[1], ' [', get_value(concat('homes.', @uuid, '.', @m[1], '.mode')), ']')))),
    #else:
        foreach(array_keys(get_values(concat('homes.', @uuid))), @name,
            assign(@m, reg_match('^homes\\..+\\.(.+)\\.loc$', @name))
            if(and(
                array_index_exists(@m,0),
                equals('public', get_value(concat('homes.', @uuid, '.', @m[1], '.mode')))),
                
                msg(concat(color('gray'), '  ', @m[1])))))
   
<<<
