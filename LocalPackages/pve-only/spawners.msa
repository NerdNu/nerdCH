#
# Creates a blaze spawner with boosted player range.
#
nerdch.level.admin:/quadblaze = >>>
    run('/setblock ~ ~-1 ~ minecraft:spawner{SpawnData:{id:"minecraft:blaze"},RequiredPlayerRange:38,MaxNearbyEntities:15} replace');
<<<

#
# Creates an iron golem spawner.
#
nerdch.level.admin:/defaultgolemspawner = >>>
    run('/setblock ~ ~-1 ~ minecraft:spawner{SpawnData:{entity:{id:iron_golem}},SpawnCount:3,SpawnRange:4,RequiredPlayerRange:25,Delay:20,MinSpawnDelay:9600,MaxSpawnDelay:9600,MaxNearbyEntities:25} replace');
<<<

#
# Creates an iron golem spawner with the specified tier.
#
nerdch.level.admin:/golemspawner [$tier] [$mob] = >>>
    if ($tier == '' && $mob == '') {
        msg(color('RED').'Usage: /golemspawner [tier] [mob]');
        die(color('RED').'    Tier is 1-20. Mob defaults to iron_golem.');
    }

    @t = integer($tier);
    if (!is_integer(@t)) {
        die(color('RED')."The tier must be a positive integer.");
    }
    if (@t <= 0) {
        die(color('RED')."The tier must be a positive integer.");
    } else if (@t > 20) {
        die(color('RED')."Let's not get crazy...");
    }

    @m = if($mob == '', 'iron_golem', to_lower($mob));
    @extraNBT = '';
    if (@m == 'hoglin') {
        @extraNBT = ',IsImmuneToZombification:1b';
    }
    run('/setblock ~ ~-1 ~ minecraft:spawner{SpawnCount:'.@t.',SpawnRange:4,Delay:20,MinSpawnDelay:9600,MaxSpawnDelay:9600,MaxNearbyEntities:25,RequiredPlayerRange:25,SpawnData:{entity:{id:"minecraft:'.@m.'",DeathLootTable:"minecraft:entities/iron_golem"'.@extraNBT.',CustomNameVisible:1b,PlayerCreated:0b,CustomName:"{\\"text\\":\\"Tier '.@t.'\\"}"}}} replace');
<<<
