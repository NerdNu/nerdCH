*:/sethome [$] = >>>
    _assertperm('restricted', 1)
    assign(@modes, array('public','private','unlisted'))
    assign(@m, reg_split(' ', $))
    assign(@p, strip_colors(player()))
    assign(@name,
        if(equals(@m[0], ''),
            'default',
            @m[0]))
    assign(@mode, 
        if(equals(@name, 'default'),
            'private',
            if(equals(array_size(@m), 2),
                @m[1],
                'unlisted')))
    
    if(array_contains(@modes, @mode),
        store_value(concat('homes.', @p, '.', @name, '.loc'), ploc())
        store_value(concat('homes.', @p, '.', @name, '.mode'), @mode)
        msg(concat(color(gray), 'Home set.')),
    #else:
        msg(concat(color(red), 'Usage: /sethome [name] [', array_implode(@modes,'|'), ']')))
<<<

*:/delhome [$] = >>>
    _assertperm('restricted', 1)
    if(equals($, ''),
        msg(concat(color(red), 'Usage: /delhome <name>'))
        call_alias(/listhomes),
    #else:
        assign(@str, concat('homes.', pinfo()[0], '.', $, '.loc'))
        if(has_value(@str),
            clear_value(@str)
            msg('Home' $ 'cleared.'),
        #else:
            msg(concat(color(red), 'Home ' , $, ' not found.'))
            call_alias(/listhomes)
        )
    )
<<<

*:/home [$] = >>>
    _assertperm('restricted', 1)
    assign(@m, reg_split(' ', $))
    if(equals(array_size(@m), 2),
        assign(@player, @m[0])
        array_remove(@m, 0),
    #else:
        assign(@player, strip_colors(player())))
    
    assign(@name,
        if(equals(@m[0], ''),
            'default',
            @m[0]))
    
    assign(@prefix, concat('homes.', @player, '.', @name, '.'))
    
    if(has_value(concat(@prefix, 'loc')),
        if(or(
            equals(@player, strip_colors(player())),
            array_contains(array('public', 'unlisted'), get_value(concat(@prefix, 'mode'))),
            has_permission('homes.override')),
            
            set_ploc(get_value(concat(@prefix, 'loc'))),
        #else:
            msg(concat(color(red), 'You do not have permission to visit that home'))),
    #else:
        msg(concat(color(red), 'Unknown home!'))
        call_alias(/listhomes))

<<<

*:/listhomes [$] = >>>
    _assertperm('restricted', 1)
    assign(@player,
        if(equals($, ''),
            strip_colors(player()),
            $))
    
    msg(concat(color(gray), if(equals(@player, strip_colors(player())), 'Your', concat(@player, '\'s')), ' homes:'))
    if(or(
        equals(@player, strip_colors(player())),
        has_permission('homes.override')),
        
        
        foreach(array_keys(get_values(concat('homes.', @player))), @name,
            assign(@m, reg_match('^homes\\..+\\.(.+)\\.loc$', @name))
            if(array_index_exists(@m,0),
                msg(concat(color(gray), '  ', @m[1], ' [', get_value(concat('homes.', @player, '.', @m[1], '.mode')), ']')))),
    #else:
        foreach(array_keys(get_values(concat('homes.', @player))), @name,
            assign(@m, reg_match('^homes\\..+\\.(.+)\\.loc$', @name))
            if(and(
                array_index_exists(@m,0),
                equals('public', get_value(concat('homes.', @player, '.', @m[1], '.mode')))),
                
                msg(concat(color(gray), '  ', @m[1])))))
   
<<<
