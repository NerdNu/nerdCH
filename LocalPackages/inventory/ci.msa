*:/ci [$] = >>>

	@ci = closure(@player) {
		@inv = pinv(@player);
		console("Clearing @player's inventory: @inv");
		array_iterate(@inv, closure(@key, @value) { @inv[@key] = null; });
		set_pinv(@player, @inv);
	};

	if ($ == '') {
		if((!array_contains(pgroup(), 'modmode') || array_contains(pgroup(), 'super')) && !import('clearinvdoublecheck'.player())) {
			export('clearinvdoublecheck'.player(), true);
			if(array_contains(pgroup(), 'super')) {
				die(color('LIGHT_PURPLE').'Cannot determine if you are in modmode, are you certain you want to clear your inventory? Run the command again in 5 seconds to confirm.');
			}
			die(color('LIGHT_PURPLE').'You are not in modmode, are you certain you want to clear your inventory? Run the command again in 5 seconds to confirm.');
		}
		set_timeout(5000, closure() {
			export('clearinvdoublecheck'.player(), null);
		});
		# Staff and defaults with nerdch.commands.ci on Creative can clear their own inv.
		if (!has_permission('nerdch.level.restricted') && !has_permission('nerdch.commands.ci')) {
			_noperm();
		}
		execute(player(), @ci);
		msg(color('GRAY') . 'Your inventory has been cleared.');
	} else {
		if (!has_permission('nerdch.level.restricted')) {
			_noperm();
		}
		if (ponline($)) {
			execute(player($), @ci);
			run_cmd('/mb Inventory of ' . $ . ' cleared.');
		} else {
			msg(color('RED') . 'Player ' . $ . ' is not online!');
		}
	}
<<<

*:/empty [$] = call_alias(/ci $)
