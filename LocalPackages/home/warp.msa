*:/setwarp [$] = >>>
    _assertperm('restricted', 0)
    if(equals($, ''),
        msg(concat(color(red), 'Usage: /setwarp <name>')),
    #else:
        store_value(concat('warps.', $), ploc())
        msg(concat(color(gray), 'Warp set!')))
<<<

*:/delwarp [$] = >>>
    _assertperm('restricted', 0)
    if(equals($, ''),
        msg(concat(color(red), 'Usage: /delwarp <name>')),
    #else:
        clear_value(concat('warps.', $))
        msg(concat(color(gray), 'Warp deleted!')))
<<<

*:/warp [$] = >>>
    _assertperm('restricted', 1)
    
    assign(@o, concat('Available warps:', color(gray)))
    
    if(equals($, ''),
        foreach(array_keys(get_values('warps')), @name,
            assign(@o, concat(@o, ' ', substr(@name, 6))))
        msg(@o),
    #else:
        assign(@found, 0)
        foreach(array_keys(get_values('warps')), @name,
            if(equals_ic($, substr(@name, 6)),
                assign(@found, 1)
                set_ploc(get_value(@name))))
        if(equals_ic(@found, 0),
            msg(concat(color(red), 'Warp not found!'))
            call_alias('/warp')))
<<<

*:/warpto [$player] [$] = >>>
    _assertperm('mod', 0)
    if(or(equals($player, ''), equals($, '')),
        msg(concat(color(red), 'Usage: /warpto <player> <warp>')),
    #else:
        if(ponline($player),
            assign(@found, 0)
            foreach(array_keys(get_values('warps')), @name,
                if(equals_ic($, substr(@name, 6)),
                    assign(@found, 1)
                    set_ploc($player, get_value(@name))
                    msg('Teleported' $player 'to' @name)
                )
            )
            if(equals_ic(@found, 0),
                msg(concat(color(red), 'Warp' substr(@name, 6) 'not found!'))
                call_alias('/warp')
            ),
        #else:
            msg(concat(color(red), 'Player' $player 'is not online!')),
        )
    )
 <<<
