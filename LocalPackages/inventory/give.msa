# commandhelper.alias.give
give:/i $mat [$qty=1] = >>>
    if ($mat == 'WATER') {
        run('/sb unsafe water');
        die();
    }
    if ($mat == 'LAVA') {
        run('/sb unsafe lava');
        die();
    }
    try {
        @item = _parse_item($mat, $qty)
        @success = pgive_item(@item);
        if (@success > 0) {
            msg(color('GRAY').'Free some space in your inventory!');
        } else {
            msg(color('GRAY').'Gave '.@item['qty'].' of '.@item['name']);
        }
    } catch (FormatException @e) {
        msg(color('GRAY').'No item matches "'.$mat.'"!')
    } catch (CastException @e) {
        msg(color('GRAY').'Usage: /i <material> [quantity]');
    }
<<<

*:/give $player $mat [$qty=1] = >>>
	_assertperm('restricted', 0);
	try {
	    @recipient = player($player);
	    @item = _parse_item($mat, $qty)
	    @success = pgive_item(@recipient, @item);
        if (@success > 0) {
            msg(color('GRAY').'That player has no space in their inventory!');
        } else {
            msg(color('GRAY').'Gave '.@item['qty'].' of '.@item['name'].' to '.@recipient);
        }
    } catch (FormatException @e) {
        msg(color('GRAY').'No item matches "'.$mat.'"!')
    } catch (PlayerOfflineException @e) {
        msg(color('GRAY').'That player is offline!');
    } catch (CastException @e) {
        msg(color('GRAY').'Usage: /i <material> [quantity]');
    }
<<<

# commandhelper.alias.give
give:'/give-that' = >>>
	if (get_block(pcursor()) == 'AIR') {
		die('No block in sight!')
	} else {
		call_alias('/i '.to_upper(get_block(pcursor())))
	}
<<<
