
*:/bitchslap $ = >>>
	run_cmd('/ban' $)
<<<
/*
*:/ban $player [$] = >>>
	_assertperm('mod', 0)
	if (_contains('nerd.nu/appeal', $)) {
		runas(player(), /ban $)
	} else {
		runas(player(), /ban $ 'nerd.nu/appeal')
	}
<<<
*/

*:/rbban $p $ = >>>
	call_alias(concat('/lbrb ', $p))
	call_alias(concat('/ban ', $p, ' ', $))
<<<


# /warncrops player [message]
#	Warn a player for crop grief and add a note.
#	The player name must match exactly.
#	Additional arguments after the player name are appeneded to the warning.

*:/warncrops $player [$] = >>>
	_assertperm('mod');
	@server = import('sharedata.source');
	@additional = $;

	@warning = 'If you take crops planted by someone else, you must replant them. ' . 
				'That includes: wheat, carrots, potatoes, beetroot, nether warts, sugar cane, cactus and cocoa.';
	if (@additional != '') {
		@warning .= ' ' . @additional;
	}
	@warning .= ' Please read http://nerd.nu/help/pve/#grief.';

	run_cmd('/addnote ' . $player . ' warned for crop grief on ' . @server);
	call_alias('/warn ' . $player . ' ' . @warning);
	msg(color(GOLD) . $player . ' received the following warning: ');
	msg(color(RED) . @warning);
<<<


# /warngrief player [message]
# 	Warn a player for general grief by mail and add a note.
#	The player name must match exactly.
#	Additional arguments after the player name are appeneded to the warning.

*:/warngrief $player [$] = >>>
	_assertperm('mod');
	@server = import('sharedata.source');
	@additional = $;

	@warning = 'You must not edit other players\' builds without permission.';
	if (@server == 'survival') {
		@warning .= ' You can break into a base to fight, but you must put back the blocks immediately.';
	} else if (@server == 'creative') {
		@warning .= ' Only staff members are allowed to clear old builds.';
	} else if (@server == 'pve') {
		@warning .= ' Do not steal blocks. There is no such thing as an "abandoned" build.';
	}

	if (@additional != '') {
		@warning .= ' ' . @additional;
	}
	@warning .= ' Please read http://nerd.nu/help/pve/#grief.';

	run_cmd('/addnote ' . $player . ' warned for general grief on ' . @server);
	call_alias('/warn ' . $player . ' ' . @warning);
	msg(color(GOLD) . $player . ' received the following warning: ');
	msg(color(RED) . @warning);
<<<


# /warncombat player [message]
# 	Warn a player for combat logging.
#	The player name must match exactly.
# 	Additional arguments after the player name are an extra mail message.

*:/warncombat $player [$] = >>>
	_assertperm('admin')
	@server = import('sharedata.source')
	if (@server != 'survival') {
		msg(color(RED).'Combat logging on '.@server.'? Really?')
	} else {
		@additional = $
		run_cmd('/addnote '.$player.' Warned for combat logging on Survival')
		call_alias('/warn '.$player.' This is a warning for your recent combat logging.')
		call_alias('/warn '.$player.' Any abuse of login to evade or gain an edge in combat is against the rules.')
		call_alias('/warn '.$player.' Please see nerd.nu/rules and refrain from this behavior in the future.')
		if (@additional != '') {
			call_alias('/warn '.$player.' '.@additional)
			msg(color(GOLD).'Additional warning:')
			msg(color(GOLD).@additional)
		}
	}
<<<


# /bancrops player [message]
#	Ban a player for crop grief. Additional aruments are added to the ban message after a semicolon.
#	The player name must match exactly.

*:/bancrops $player [$] = >>>
	_assertperm('mod')
	@server = import('sharedata.source')
	@message = 'crop grief on '.@server
	if ($ != '') {
		@message = @message.'; '.$
	}
	call_alias('/ban '.$player.' '.@message)
<<<


# /rbbancrops player [message]
#	Ban the player for crop grief (as with /bancrops) and do a full global rollback.

*:/rbbancrops $player [$] = >>>
	call_alias(concat('/bancrops ', $player, ' ', $))
	call_alias(concat('/lbrb ', $player))
<<<


# /bangrief player [message]
#	Ban a player for general grief. Additional aruments are added to the ban message after a semicolon.
#	The player name must match exactly.

*:/bangrief $player [$] = >>>
	_assertperm('mod')
	@server = import('sharedata.source')
	@message = 'grief on '.@server
	if ($ != '') {
		@message = @message.'; '.$
	}
	call_alias('/ban '.$player.' '.@message)
<<<


# /rbbangrief player [message]
#	Ban the player for general grief (as with /bangrief) and do a full global rollback.

*:/rbbangrief $player [$] = >>>
	call_alias(concat('/bangrief ', $player, ' ', $))
	call_alias(concat('/lbrb ', $player))
<<<
